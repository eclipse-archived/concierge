import org.apache.tools.ant.filters.ReplaceTokens

// we change the version "6.x.y.qualifier" by the current build version
// note: if this will change also update the configuration here
// See http://stackoverflow.com/questions/30038540/replace-token-in-file-before-building-but-keep-token-in-sources
def buildVersionFile = 'src/org/eclipse/concierge/Concierge.java'
def buildVersionFileName = 'Concierge.java'
def buildVersionTempDir = 'build/tmp/sourcesCache'
def buildVersionPattern = '6.0.0.qualifier'

compileJava.doFirst {
    copy {
        from(buildVersionFile)
        into(buildVersionTempDir)
    }
    // we use buildVersion to inject
    ant.replace(file: buildVersionFile, token: buildVersionPattern, value: buildVersion)
    copy {
        from(buildVersionFile)
        into(buildVersionTempDir + '/' + buildVersionFileName + '.gen')
    }
}

compileJava.doLast {
    copy {
        from(buildVersionTempDir + '/' + buildVersionFileName)
        into(project.file(buildVersionFile).parent)
    }
}

// add . folder to test classpath, to lookup META-INF/micro-services
sourceSets {
    test {
	    runtimeClasspath += files('.')
    }
}

test {
    // forward a system property for the test JVM(s)
    systemProperty "org.eclipse.concierge.tests.waitAfterFrameworkShutdown", 
    	System.getProperty("org.eclipse.concierge.tests.waitAfterFrameworkShutdown")
}


// make stripped jar with no debug information
// pack200 --repack --strip-debug <stripped-jar> <original-jar>
task strippedJar(type: Exec, dependsOn: jar) {
    def inputFile = new File(jar.destinationDir, jar.archiveName)
    def outputFile = new File(jar.destinationDir, jar.archiveName.replace('.jar', '-nodebug.jar'))

	outputs.files outputFile

    commandLine 'pack200', '--repack', '--strip-debug', outputFile.absolutePath, inputFile.absolutePath
}

publishing {
    publications {
   	    mavenJava(MavenPublication) {
	   	    groupId 'org.eclipse.concierge'
            artifactId 'org.eclipse.concierge'
            version version
       	    from components.java
	       	artifact sourceJar {
                classifier "sources"
            }
       	}
   	}
}
